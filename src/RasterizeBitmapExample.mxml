<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" 
			   minWidth="955" minHeight="600"
			   creationComplete="creationCompleteHandler()">
	<fx:Script>
		<![CDATA[
			import com.elad.optimize.cache.RasterizeBitmap;
			import flash.filters.DropShadowFilter;
			import mx.core.UIComponent;
			
			protected function creationCompleteHandler():void
			{
				var statsSprite:Stats = new Stats();
				var componenent:UIComponent = new UIComponent();
				
				componenent.addChild( statsSprite );
				stats.addElement( componenent );
			}			
			
			protected function manualCache():void
			{
				var source:Sprite = spriteSource;
				var destination:Sprite = new Sprite();
				
				var uicom:UIComponent = new UIComponent();
				uicom.addChild(destination);
				group.addElement( uicom );
				
				var bitmap:Bitmap;
				for (var i:int = 0; i < 1000; i++) 
				{
					var bitmapData:BitmapData = RasterizeBitmap.cacheAsBitmapData( source );
					bitmap = createBitmapClip( bitmapData );
					destination.addChild( bitmap ); 
				}				
			}
			
			private function bitmapCache():void
			{
				var destination:Sprite = new Sprite();
				var uicom:UIComponent = new UIComponent();
				uicom.addChild(destination);
				group.addElement( uicom );

				var movieClip:MovieClip;
				for (var i:int = 0; i< 1000; i++ )
				{
					movieClip = createMovieClip();
					movieClip.cacheAsBitmap = true;
					destination.addChild( movieClip ); 
				}
			}
			
			private function get spriteSource():Sprite
			{
				var source:Sprite = new Sprite();
				source.graphics.beginFill(0xFF88CC);
				source.graphics.drawRect(0, 0, 80, 80);
				
				source.filters = [new DropShadowFilter()];	
				
				return source;
			}
			
			private function createMovieClip():MovieClip
			{
				var movieClip:MovieClip = new MovieClip();
				var source:Sprite = spriteSource;
				
				movieClip.addChild( source );
				movieClip.cacheAsBitmap = true;
				setClipEventHandlers( movieClip );
				
				return movieClip;
			}
			
			private function createBitmapClip(buffer:BitmapData):Bitmap
			{
				var movieClip:Bitmap = new Bitmap(buffer);
				setClipEventHandlers( movieClip );
				
				return movieClip;
			}
			
			private function setClipEventHandlers(clip:*):void
			{
				var newX:int;
				var newY:int;	
				
				clip.addEventListener( Event.ADDED_TO_STAGE, function( event:Event ):void {
					
					newX = Math.random() * ( stage.stageWidth - ( clip.width / 2 ) );
					newY = Math.random() * ( stage.stageHeight - (clip.height / 2 ) );
					
					clip.addEventListener( Event.ENTER_FRAME, function( event:Event ):void {
						
						clip.x -= ( clip.x - newX ) * 0.25;
						clip.y -= ( clip.y - newY ) * 0.25;
						clip.alpha = Math.random();
						
					});
				});	
			}

		]]>
	</fx:Script>
	
	<s:Group id="group" />
	
	<s:Group id="stats" />
	<s:Button label="Bitmap Cache" click="group.removeAllElements(); bitmapCache()" x="288" y="4"/>
	<s:Button label="Manual Cache" click="group.removeAllElements(); manualCache()" x="387" y="4"/>

</s:Application>
